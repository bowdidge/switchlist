#!/bin/csh
#
# Build the SwitchList.app distribution and put everything in a single 
# Disk Image.
#
# Get the version by searching the CVS log for a file for the tag names. 
# Pick the first tag name, and grab the name portion.
#
# TODO(bowdidge) Redo for SVN

if $#argv != 1 then
  echo Usage: makeDistribution SwitchList.x.y.z
  exit 1
endif

set DMG_NAME=$1

echo Disk Image name will be $DMG_NAME
rm -rf /tmp/SwitchList /tmp/$DMG_NAME.dmg
rm -f /tmp/SwitchList.tar.gz /tmp/SwitchList.tar
rm -rf /tmp/switchlist-read-only

# /tmp/SwitchList is where the disk image will be staged.
mkdir /tmp/SwitchList

# /tmp/switchlist-read-only contains the checked-out version of the sources.
# TODO(bowdidge) Check out the specific tag.
svn checkout http://switchlist.googlecode.com/svn/tags/$1 /tmp/switchlist-read-only
if $status != 0 then
  echo "Problems checking out svn sources - non-existent tag $1?"
  exit 1
endif

pushd /tmp/switchlist-read-only/src
# Fat x86/ppc binary.
xcodebuild -target SwitchList -configuration Release  clean
xcodebuild -target SwitchList -configuration Release  SYMROOT=/tmp/XcodeBuildProducts OBJROOT=/tmp/XcodeBuildIntermediates
if $status != 0 then
  echo Problems building SwitchList
  exit 1
endif
popd 

cp -r /tmp/XcodeBuildProducts/Release/SwitchList.app /tmp/SwitchList
cp -r examples /tmp/SwitchList/Examples
cp -r template_examples /tmp/SwitchList/Custom\ Switchlist\ Examples

pushd /tmp/
hdiutil create ./$DMG_NAME.dmg -srcfolder SwitchList/ -ov
popd

# Make sure it's a fat binary.
file /tmp/Switchlist/SwitchList.app/Contents/MacOS/SwitchList | grep "ppc"
if $status != 0 then
  echo "Missing PowerPC binary!"
  exit 1
endif

echo "/tmp/$DMG_NAME.dmg has the final disk image."
